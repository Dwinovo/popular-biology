plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
    include "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
    modCompileOnly "maven.modrinth:jade:${jade_version_fabric}"
    modRuntimeOnly "maven.modrinth:jade:${jade_version_fabric}"
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
        datagen {
            inherit client
            setConfigName('Fabric Data Generation')
            ideConfigGenerated(true)
            runDir('runs/datagen')
            vmArg("-Dfabric-api.datagen")
            vmArg("-Dfabric-api.datagen.output-dir=${file('src/generated/resources')}")
            vmArg("-Dfabric-api.datagen.modid=${mod_id}")
        }
    }
}

// Implement mcgradleconventions loader attribute
def loaderAttribute = Attribute.of('io.github.mcgradleconventions.loader', String)
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements', 'includeInternal', 'modCompileClasspath'].each { variant ->
    configurations.named("$variant") {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}
sourceSets.configureEach {
    [it.compileClasspathConfigurationName, it.runtimeClasspathConfigurationName].each { variant->
        configurations.named("$variant") {
            attributes {
                attribute(loaderAttribute, 'fabric')
            }
        }
    }
}
loom.remapConfigurations.configureEach {
    configurations.named(it.name) {
        attributes {
            attribute(loaderAttribute, 'fabric')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
